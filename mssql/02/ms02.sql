SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
 
DECLARE @UUIDS TABLE (UUID UNIQUEIDENTIFIER)
INSERT INTO @UUIDS (UUID)
VALUES
--UUID(s) lits to search 
('00000000-0000-0000-0000-000000000000'),
('11111111-1111-1111-1111-111111111111')
 
DECLARE @STRSQL NVARCHAR(MAX),
    @SEARCHGUID UNIQUEIDENTIFIER,
    @LOWROW SMALLINT,
    @SRCHEVTTABLES BIT,
    @SRCHCHARTYPES BIT,
    @SRCHTEXTTYPES BIT,
    @CHARTYPECOLNAME VARCHAR(200)
 
SET @SRCHEVTTABLES = 1
SET @SRCHCHARTYPES = 1
SET @CHARTYPECOLNAME = LOWER('%ID')
SET @SRCHTEXTTYPES = 0
 
DECLARE @TABLESTOBESEARCHED TABLE
(
    ROWID SMALLINT IDENTITY(1,1),
    TABLENAME VARCHAR(255),
    COLNAME VARCHAR(128),
    ISGUIDTYPE BIT,
    SQLQUERY NVARCHAR(MAX)
)
 
DECLARE @UUID UNIQUEIDENTIFIER
 
DECLARE cur CURSOR FOR SELECT UUID FROM @UUIDS
OPEN cur
FETCH NEXT FROM cur INTO @UUID
 
WHILE @@FETCH_STATUS = 0
BEGIN
    INSERT INTO @TABLESTOBESEARCHED
    SELECT [TABLE_NAME],
        [COLUMN_NAME],
        1 [ISGUIDTYPE],
        'FROM [' + [TABLE_NAME] + '] WHERE [' + [COLUMN_NAME] + '] = ''' + CAST(@UUID AS VARCHAR(38)) + ''''
    FROM
    (
        SELECT ISC.[TABLE_NAME],
            ISC.[COLUMN_NAME],
            CASE
                WHEN SUBSTRING(ISC.[TABLE_NAME], 1, 4) = 'EVT_'
                    THEN 1
                ELSE 0
            END [ISEVENTTABLE]
        FROM INFORMATION_SCHEMA.COLUMNS ISC
            INNER JOIN INFORMATION_SCHEMA.TABLES IST
                ON ISC.[TABLE_NAME] = IST.[TABLE_NAME]
        WHERE ISC.[DATA_TYPE] = 'UNIQUEIDENTIFIER'
            AND IST.[TABLE_TYPE] = 'BASE TABLE'
    ) T1
    WHERE [ISEVENTTABLE] = 0
        OR ([ISEVENTTABLE] = 1 AND @SRCHEVTTABLES = 1)
 
    IF (@SRCHCHARTYPES = 1)
    BEGIN
        INSERT INTO @TABLESTOBESEARCHED
        SELECT [TABLE_NAME],
            [COLUMN_NAME],
            0 [ISGUIDTYPE],
            'FROM [' + [TABLE_NAME] + '] WHERE LOWER([' + [COLUMN_NAME] + ']) LIKE ''%' + LOWER(CAST(@UUID AS VARCHAR(38))) + '%'''
        FROM
        (
            SELECT ISC.[TABLE_NAME],
                ISC.[COLUMN_NAME],
                CASE
                    WHEN SUBSTRING(ISC.[TABLE_NAME], 1, 4) = 'EVT_'
                        THEN 1
                    ELSE 0
                END [ISEVENTTABLE]
            FROM INFORMATION_SCHEMA.COLUMNS ISC
                JOIN INFORMATION_SCHEMA.TABLES IST
                    ON ISC.[TABLE_NAME] = IST.[TABLE_NAME]
            WHERE ISC.[DATA_TYPE] IN
            (
                'CHAR',
                'NCHAR',
                'VARCHAR',
                'NVARCHAR'
            )
                AND LOWER(LTRIM(RTRIM(ISC.[COLUMN_NAME]))) LIKE LTRIM(RTRIM(@CHARTYPECOLNAME))
                AND IST.[TABLE_TYPE] = 'BASE TABLE'
                AND ISC.[CHARACTER_MAXIMUM_LENGTH] >= 38
        ) T1
        WHERE [ISEVENTTABLE] = 0
            OR ( [ISEVENTTABLE] = 1 AND @SRCHEVTTABLES = 1)
    END
 
    IF @SRCHTEXTTYPES  = 1
    BEGIN
        INSERT INTO @TABLESTOBESEARCHED
        SELECT [TABLE_NAME],
            [COLUMN_NAME],
            NULL [ISGUIDTYPE],
            'FROM [' + [TABLE_NAME] + '] WHERE LOWER(CAST([' + [COLUMN_NAME] + '] AS NVARCHAR(MAX))) LIKE ''%' + LOWER(CAST(@UUID AS VARCHAR(38))) + '%'''
        FROM
        (
            SELECT ISC.[TABLE_NAME],
                ISC.[COLUMN_NAME],
                CASE
                    WHEN SUBSTRING(ISC.[TABLE_NAME], 1, 4) = 'EVT_'
                        THEN 1
                    ELSE 0
                END [ISEVENTTABLE]
            FROM INFORMATION_SCHEMA.COLUMNS ISC
                JOIN INFORMATION_SCHEMA.TABLES IST
                    ON ISC.[TABLE_NAME] = IST.[TABLE_NAME]
            WHERE ISC.[DATA_TYPE] IN ('TEXT', 'NTEXT')
                AND IST.[TABLE_TYPE] = 'BASE TABLE'
        ) T1
        WHERE [ISEVENTTABLE] = 0
            OR ( [ISEVENTTABLE] = 1 AND @SRCHEVTTABLES = 1)
    END
 
    IF (OBJECT_ID('TEMPDB..#FOUND') IS NOT NULL)
        DROP TABLE #FOUND
 
    CREATE TABLE #FOUND (ROWID SMALLINT, ROWSFOUND INT)
    SET @LOWROW = 0
 
    WHILE (@LOWROW < (SELECT MAX(ROWID) FROM @TABLESTOBESEARCHED WHERE ISGUIDTYPE IS NOT NULL))
    BEGIN
        SET @STRSQL = NULL
        SELECT @STRSQL = COALESCE(@STRSQL + ' UNION ALL SELECT ', 'INSERT INTO #FOUND
            SELECT ') + '''' + CAST(TTBS.[ROWID] AS VARCHAR(10)) + ''' [ID], COUNT( * ) [CNT] ' + TTBS.SQLQUERY
        FROM @TABLESTOBESEARCHED TTBS
        WHERE TTBS.[ROWID] BETWEEN @LOWROW + 1 AND @LOWROW + 15
            AND (ISGUIDTYPE = 1 OR @SRCHCHARTYPES = 1)
         EXEC(@STRSQL)
        SET @LOWROW = @LOWROW + 15
    END
 
    SET @LOWROW = (SELECT MAX(ROWID) FROM @TABLESTOBESEARCHED WHERE ISGUIDTYPE IS NOT NULL)
    IF (@SRCHTEXTTYPES  = 1)
    BEGIN
        WHILE (@LOWROW < (SELECT MAX(ROWID) FROM @TABLESTOBESEARCHED WHERE ISGUIDTYPE IS NULL))
        BEGIN
            SET @STRSQL = NULL
 
            SELECT @STRSQL = COALESCE(@STRSQL + ' UNION ALL SELECT ', 'INSERT INTO #FOUND
                SELECT ') + '''' + CAST(TTBS.[ROWID] AS VARCHAR(10)) + ''' [ID]
                , COUNT( * ) [CNT] ' + TTBS.SQLQUERY
            FROM @TABLESTOBESEARCHED TTBS
            WHERE TTBS.[ROWID] BETWEEN @LOWROW + 1 AND @LOWROW + 1
                AND TTBS.[ISGUIDTYPE] IS NULL
 
            EXEC(@STRSQL)
 
            SET @LOWROW = @LOWROW + 1
        END
    END
 
    SELECT DISTINCT TTBS.[TABLENAME],
        TTBS.[COLNAME],
        F.[ROWSFOUND],
        'SELECT * ' + TTBS.SQLQUERY
    FROM #FOUND F
        JOIN @TABLESTOBESEARCHED TTBS
            ON F.[ROWID] = TTBS.[ROWID]
    WHERE ROWSFOUND > 0
 
    DELETE FROM @TABLESTOBESEARCHED
    FETCH NEXT FROM cur INTO @UUID
END
 
CLOSE cur
DEALLOCATE cur
